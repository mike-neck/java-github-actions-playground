plugins {
    id 'java-library'
    id 'maven-publish'
    id "com.github.breadmoirai.github-release" version "2.2.9"
}

ext {
    projectVersion = project.hasProperty('project_version') ?
            "${project.property('project_version')}".trim() : './get-git-version.sh'.execute().text.trim()

    githubUser = project.hasProperty('github_user') ?
            "${project.property('github_user')}".trim() : ''
    githubToken = "${System.getenv('GITHUB_TOKEN')}"
}

version = projectVersion
group = 'org.mikeneck.example'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'io.projectreactor:reactor-core:3.3.2.RELEASE'
    testImplementation 'io.projectreactor:reactor-test:3.3.2.RELEASE'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.4.2'
    testImplementation 'org.slf4j:slf4j-simple:1.7.30'
}

test {
    useJUnitPlatform()
}


githubRelease {
    token githubToken
    owner 'mike-neck'
    repo project.name
    tagName projectVersion
    targetCommitish 'master'
    releaseName projectVersion
    releaseAssets {
        tasks.jar.outputs.files.files
    }
    overwrite false
    body {
        file("release-notes/${projectVersion}.txt").text
    }
}

tasks.githubRelease.dependsOn(tasks.jar)
tasks.githubRelease.group('release')

task showVersion(group: 'release') {
    doLast {
        logger.lifecycle "projectVersion: $projectVersion"
    }
}

task verifyRelease(group: 'release') {
    doLast {
        if(!file("release-notes/${projectVersion}.txt").exists()) {
            throw new IllegalStateException("Release notes(release-notes/${projectVersion}.txt) is not existing. Please prepare release notes.")
        }
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    repositories {
        maven {
            name = 'GithubPackages'
            url 'https://maven.pkg.github.com/mike-neck/java-github-actions-playground'
            credentials {
                username = githubUser
                password = githubToken
            }
        }
    }
    publications {
        github(MavenPublication) {
            from(components.java)
            pom {
                name = 'java-github-actions-playground'
                description = 'java github actions example'
                url = 'https://github.com/mike-neck/java-github-actions-playground'
                scm {
                    connection = 'scm:git:git://github.com/mike-neck/java-github-actions-playground.git'
                    developerConnection = 'scm:git:git://github.com/mike-neck/java-github-actions-playground.git'
                    url = 'https://github.com/mike-neck/java-github-actions-playground'
                }
            }
        }
    }
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}
