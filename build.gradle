plugins {
    id 'java-library'
    id "com.github.breadmoirai.github-release" version "2.2.9"
}

ext {
    projectVersion = project.hasProperty('project_version') ? "${project.property('project_version')}" : './get-git-version.sh'.execute().text
}

version = projectVersion
group = 'org.mikeneck.example'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'io.projectreactor:reactor-core:3.3.2.RELEASE'
    testImplementation 'io.projectreactor:reactor-test:3.3.2.RELEASE'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.4.2'
    testImplementation 'org.slf4j:slf4j-simple:1.7.30'
}

test {
    useJUnitPlatform()
}


githubRelease {
    token {
        System.getenv('GITHUB_TOKEN')
    }
    owner 'mike-neck'
    repo project.name
    tagName projectVersion
    targetCommitish 'master'
    releaseName projectVersion
    releaseAssets {
        tasks.jar.outputs.files.files
    }
    overwrite true
    body changelog {
        currentCommit 'HEAD'
        lastCommit 'dd5fa78dc3239e761c2400c522e5bbb0d6b6817a'
        options(["--format=oneline", "--abbrev-commit", "--max-count=50"])
    }
}

tasks.githubRelease.dependsOn(tasks.jar)

task revList(type: Exec) {
    executable('git')
    args 'rev-list', '--format=oneline', '--abbrev-commit', '--max-count=50', 'dd5fa78dc3239e761c2400c522e5bbb0d6b6817a..HEAD', '--'
//    standardOutput(System.out)
}
